/**
 * @file convert.idl
 * @author Jeff Parsons <j.parsons@vanderbilt.edu>
 *
 * Tests all conversions of IDL3 constructs to IDL2.
 */

#include <Components.idl>

#if !defined (CCM_NOEVENT)
eventtype eattype;
#endif /* !CCM_NOEVENT */
interface sface {};
interface pface {};
interface uface {};
interface umface {};

#if !defined (CCM_LW)
valuetype pkey : Components::PrimaryKeyBase
{
  public string info;
};
#endif

#if !defined (CCM_NOEVENT)
valuetype pubtype_base {};
eventtype pubtype : pubtype_base {};
eventtype emitype : pubtype {};
eventtype eattype {};
#endif /* !CCM_NOEVENT */

component CompBase supports sface
{
};

component CompDerived : CompBase
{
  provides pface pface_provider;
  uses uface uface_user;
  uses multiple umface umface_umuser;
#if !defined (CCM_NOEVENT)
  publishes pubtype pubtype_publisher;
  emits emitype emitype_emitter;
  consumes eattype eattype_consumer;
#endif /* !CCM_NOEVENT */
};

home CompBaseHome supports sface manages CompBase
{
};

#if !defined (CCM_LW)
home CompDerivedHome : CompBaseHome manages CompDerived primarykey pkey
{
  exception ch_except {};
  void ch_op () raises (ch_except);
  factory def_init ();
  finder kitchen_sink (in string uid, in pkey key, in boolean all)
    raises (ch_except);
};
#endif

